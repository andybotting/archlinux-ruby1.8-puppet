# Puppet: Installer: Arch
# Maintainer : Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>

pkgname=ruby1.8-puppet
pkgver=2.7.19
pkgrel=2
pkgdesc="A system for automating system administration tasks."
arch=("any")
url="http://puppetlabs.com/projects/puppet/"
license=("APACHE")
depends=("ruby1.8" "ruby1.8-shadow" "ruby1.8-facter")
backup=("etc/puppet/puppet.conf")
options=(emptydirs)
install="puppet.install"
source=("http://puppetlabs.com/downloads/puppet/puppet-$pkgver.tar.gz"
        "puppet"
        "puppetmaster"
        "puppetmaster.service"
        "puppet.conf"
        "puppet.service")
md5sums=('ba76fa7831df53db7424a8d523465ef5'
         '59dbf39e251bc4877e7604a5876c642d'
         '4c507c580cb28d7a5adc9ea6b3626657'
         '7af26262ce01900a560ab02e7a38a91c'
         'a6c0ce2eb12d62fdd3421aa9dff3477d'
         'e9c544886335d0dd9b0f6f669cee83eb')

package() {
  cd $srcdir/puppet-$pkgver

  # Install
  ruby-1.8 ./install.rb \
    --destdir=$pkgdir \
    --bindir=/usr/bin \
    --sbindir=/usr/bin \
    --mandir=/usr/share/man

  # Set up vim and emacs
  install -Dp -m0644 ext/vim/ftdetect/puppet.vim $pkgdir/usr/share/vim/vimfiles/ftdetect/puppet.vim
  install -Dp -m0644 ext/vim/syntax/puppet.vim $pkgdir/usr/share/vim/vimfiles/syntax/puppet.vim
  install -Dp -m0644 ext/emacs/puppet-mode.el $pkgdir/usr/share/emacs/site-lisp/puppet-mode.el
  install -Dp -m0644 ext/emacs/puppet-mode-init.el $pkgdir/usr/share/emacs/site-lisp/site-start.d/puppet-mode-init.el
  
  # Configuration and rc scripts
  install -d $pkgdir/etc/{puppet,rc.d}
  install -m 644 $srcdir/puppet.conf $pkgdir/etc/puppet/puppet.conf

  # Systemd files
  install -Dm 644 $srcdir/puppet.service $pkgdir/usr/lib/systemd/system/puppet.service
  install -Dm 644 $srcdir/puppetmaster.service $pkgdir/usr/lib/systemd/system/puppetmaster.service

  # Create puppet homedir
  install -d $pkgdir/var/lib/puppet

  install -D $srcdir/puppet $pkgdir/etc/rc.d/puppet
  install -D $srcdir/puppetmaster $pkgdir/etc/rc.d/puppetmaster

  # Move pi to prevent file conflict
  mv $pkgdir/usr/bin/pi $pkgdir/usr/bin/puppet-pi

  # Copy the LICENSE file in the usual directory
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

}
