# Maintainer: Jochen Schalanda <jochen+aur@schalanda.name>
_gemname=hiera-puppet
pkgname=ruby1.8-$_gemname
pkgver=1.0.0
pkgrel=1
pkgdesc='Store and query Hiera data from Puppet'
arch=(any)
url='http://projects.puppetlabs.com/projects/hiera/'
license=('MIT')
depends=('ruby1.8' 'ruby-hiera1.8')
makedepends=('rubygems')
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby-1.8 -rubygems -e'puts Gem.default_dir')"

  gem-1.8 install --no-user-install --ignore-dependencies -i "$pkgdir$_gemdir" \
    "$_gemname-$pkgver.gem"
}
md5sums=('c9aa0869a45b1e719a452995db14d9fc')
